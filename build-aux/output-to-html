#! /usr/bin/perl -w

# Convert a bison automaton description file into an html file with
# some navigation aids.  Better is needed, but that's definitely
# something that bison should provide.

use strict;
use IO::File;

my $in = $ARGV[0];
my $out = $in;
$out =~ s/output$/html/;

my $inf = new IO::File ($in)
   or die "cannot open $in";

my @line = $inf->getlines()
    or die "cannot get lines";

my $outf = new IO::File (">$out")
   or die "cannot open $out";

# First pass to gather the predecessors.
my %predecessors;
{
    my $state;
    foreach (@line)
    {
	$state = $1
	    if /^state (\d+)$/;
	push @{$predecessors{$1}}, $state
	    if /go to state (\d+)/;
    }
}

# Output the result.
print $outf "<pre>\n";

foreach my $i ("Conflicts", "Grammar", "Useless rules", "States",
	       "Terminals, with rules where they appear",
	       "Nonterminals, with rules where they appear",
	       "Terminals which are not used",
	       "Useless nonterminals")
{
    print $outf "<a href=\"\#$i\">$i</a>\n";
}
print $outf "\n\n";

my $conflicts_seen = 0;

foreach (@line)
{
    chop;
    if (/^state (\d+)$/)
    {
	$_ = "<a name=\"state-$1\">$_</a>";
	if (defined $predecessors{$1})
	{
	    $_ .= "\n   Predecessors:";
	    for my $pred (@{$predecessors{$1}})
	    {
		$_ .= " <a href=\"#state-$pred\">$pred</a>"
	    }
	}
	$_ = "<a name=\"States\">States</a>\n$_"
	    if $1 == 0;
    }
    else
    {
	s/([Ss]tate) (\d+)/<a href="#state-$2">$1 $2<\/a>/g;
    }


    if (/State.*\d+.*conflicts:/ && !$conflicts_seen)
    {
	$conflicts_seen = 1;
	$_ = "<a name=\"Conflicts\">Conflicts</a>\n$_";
    }

    # Anchor to the rules in the grammar.
    if (/^Grammar$/ ... /^Terminals/)
    {
	s/^(\s+)(\d+)/$1<a name="rule-$2">$2<\/a>/;
    }
    else
    {
	# Reference a rule.
	s/(rule) (\d+)/<a href="#rule-$2">$1 $2<\/a>/g;
	s/^(\s+)(\d+)(\s)/$1<a href="#rule-$2">$2<\/a>$3/;
    }


    if (/^Grammar|Useless rules|Terminals, with rules where they appear|Nonterminals, with rules where they appear|Terminals which are not used|Useless nonterminals$/)
    {
	$_ = "<a name=\"$_\">$_</a>\n";
    }

    print $outf "$_\n";
}
print $outf "</pre>\n";

print STDERR "$out was created\n";
